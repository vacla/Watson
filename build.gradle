plugins {
	id 'fabric-loom' version '0.9-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

repositories {
	mavenCentral()
	maven { url 'https://masa.dy.fi/maven' }
	maven { url 'https://maven.terraformersmc.com/releases/' }
}

ext.configFile = file "build.properties"

configFile.withReader {
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse prop
}

minecraft {
	ext.mod_version = config.mod_version + "." + new Date().format('yyyyMMdd.HHmmss')
}

dependencies {
	minecraft "com.mojang:minecraft:" + config.minecraft_version
	mappings "net.fabricmc:yarn:" + config.mappings_version + ":v2"
	modImplementation "net.fabricmc:fabric-loader:" + config.fabric_loader_version
	implementation "com.google.code.findbugs:jsr305:3+"

	modImplementation "fi.dy.masa.malilib:malilib-fabric-" + config.minecraft_version_out + ":" + config.malilib_version
	modImplementation "com.terraformersmc:modmenu:" + config.mod_menu_version
}

group = config.group + "." +config.mod_id // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = config.mod_file_name + '-' + config.minecraft_version_out
version = project.minecraft.mod_version

compileJava {
	sourceCompatibility = JavaVersion.VERSION_16
	targetCompatibility = JavaVersion.VERSION_16
	options.encoding = 'UTF-8'
}

processResources {

	exclude '**/*.xcf'
	exclude '**/xcf'

	// this will ensure that this task is redone when the versions change.
	inputs.property "mod_version", project.minecraft.mod_version

	filesMatching("fabric.mod.json") {
		expand "mod_version": project.minecraft.mod_version
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}